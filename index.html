<!DOCTYPE html>
<html>
	<head>
		<meta charset="utf-8">
		<title>First Person Shooter Game</title>
		<style>
		    body{margin: 0; padding: 0; font-family: sans-serif;cursor: crosshair;overflow: hidden;}
			#scoreboard {
				position: absolute;
				top: 0;
				right: 0;
				width: 200px;
				margin: 15px auto;
				text-align: left;
				color: #757575; font-family: cursive; font-weight: bolder; font-size: 16px; display: none;}
			#health_bar, #health_status{width: 100px; height: 15px; border: 2px solid grey; border-radius: 3px; }
			#health_status{width: 100px;background: red; border: none;}
			#instruction, #pause  { position: absolute;
				top: 0; left: 0; right: 0; bottom: 0;
				background: grey;
				opacity: 0.8;
				text-align: center;
				color: #fff;
				z-index: 100; 
			}

			#instruction h2, #pause h2{margin-top: 100px; font-family: cursive; font-weight: bolder; font-size: 26px;}
			#instruction h3{margin-top: 25px; font-family: cursive; font-weight: bolder; font-size: 26px; color: #d04c4c;}
			#instruction .wrap{text-align: left;  width: 300px; margin: 0 auto;}
			button{font-size: 16px; padding: 8px; background: #d04c4c; color: #fff; border-radius: 5px; border: 1px solid #fff; margin: 0 auto; cursor: pointer;}
			#mini_map{position: absolute; top:0; left: 0; width: 200px; height: 200px; background: #73c3aa; z-index: 90;}
		    #hurt, #heal { background-color: red; display: none; position: absolute; top: 0; left: 0; right: 0; bottom: 0; opacity: 0.15; /*pointer-events: none;*/  z-index: 200;}
		    #heal  {background: green;}
		    #pause { display: none;}
		    #resume{margin: 50px auto;}
		</style>
	</head>
	<body>
		<div id='scoreboard'>
			<p>Score: <span id="score"></span> </p>
			<div class="health"> 
				Health: <span id="health"></span>
				<div id="health_bar"><div id="health_status"></div></div>
		    </div> <!--.health -->
<!-- 			<p> Time Left: <span id="time_left"></span></p> -->
		</div>
		<div id="instruction">
			<h2> First Person Shooter Game </h2>
			<div class="wrap">
				<button class="new_game">Start A New Game</button>
				<h4>How to Play:</h4>
				<p>Shoot the AIs before they crush into you<p>
				<h5>Arrow keys: Move </h5>
				<h5>Space: Pause </h5>
				<h5>Mouse Move: Look Around</h5> 
				<h5>Mouse Click: Shoot</h5>
				<h5>Shoot the Medicine Box to Gain Health</h5>

			</div><!-- .wrap -->
			<h3 id="result"></h3>
		</div><!-- #instruction -->

		<div id="pause">
			<h2> First Person Shooter Game </h2>
			<button id="resume">Resume</button>
		</div><!-- #pause -->

		<div id="mini_map"></div>

		<div id="hurt"></div>
		<div id="heal"></div>

		<!-- audio -->
		<audio id="shot_gun" src="./audio/shot_gun.mp3" ></audio>
		<audio id="blast" src="./audio/blast.mp3" ></audio>

		<!-- script -->
		<script src="https://ajax.googleapis.com/ajax/libs/jquery/2.1.3/jquery.min.js"></script>
        <script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/r72/three.min.js"></script> 
		<script src='./js/SubdivisionModifier.js'></script>
		<script src="https://cdnjs.cloudflare.com/ajax/libs/d3/3.5.5/d3.min.js"></script>
		<script src='./js/game.js'></script>

		<!--  custom Shader  for cube glow -->
		<script id="vertexShader" type="x-shader/x-vertex">
			uniform vec3 viewVector;
			uniform float c;
			uniform float p;
			varying float intensity;
			void main() 
			{
			    vec3 vNormal = normalize( normalMatrix * normal );
				vec3 vNormel = normalize( normalMatrix * viewVector );
				intensity = pow( c - dot(vNormal, vNormel), p );
				
			    gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
			}
		</script>

		<!-- fragment shader a.k.a. pixel shader for cube glow-->
		<script id="fragmentShader" type="x-shader/x-vertex"> 
			uniform vec3 glowColor;
			varying float intensity;
			void main() 
			{
				vec3 glow = glowColor * intensity;
			    gl_FragColor = vec4( glow, 0.3 );
			    // gl_FragColor = vec4( glowColor, 0.5);
			}
		</script>

		<!--  custom Shader  for fire ball-->
		<script id="fb_vertexShader" type="x-shader/x-vertex">
			varying vec3 vNormal;
			attribute float displacement;
			uniform float amplitude;

			void main() 
			{	
				vNormal = normal;
				vec3 newPosition = position + normal * vec3(displacement * amplitude);
				// vec3 newPosition = position + normal * vec3(displacement);
			    gl_Position = projectionMatrix * modelViewMatrix * vec4( newPosition, 1.0 );
			}
		</script>

		<!-- fragment shader a.k.a. pixel shader -->
		<script id="fb_fragmentShader" type="x-shader/x-vertex"> 
			uniform vec3 glowColor;
			varying vec3 vNormal;

			void main() 
			{
				vec3 light = vec3(0, -1, 0);
				light = normalize(light);

				float dProd = max(0.0, dot(vNormal, light));
			    gl_FragColor = vec4( glowColor*dProd, 1);
			}
		</script>

<!-- code for making fireball with perlin noise -->
<script type="x-shader/x-vertex" id="perlin_vertexShader">

//
// GLSL textureless classic 3D noise "cnoise",
// with an RSL-style periodic variant "pnoise".
// Author:  Stefan Gustavson (stefan.gustavson@liu.se)
// Version: 2011-10-11
//
// Many thanks to Ian McEwan of Ashima Arts for the
// ideas for permutation and gradient selection.
//
// Copyright (c) 2011 Stefan Gustavson. All rights reserved.
// Distributed under the MIT license. See LICENSE file.
// https://github.com/ashima/webgl-noise
//

vec3 mod289(vec3 x)
{
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 mod289(vec4 x)
{
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 permute(vec4 x)
{
  return mod289(((x*34.0)+1.0)*x);
}

vec4 taylorInvSqrt(vec4 r)
{
  return 1.79284291400159 - 0.85373472095314 * r;
}

vec3 fade(vec3 t) {
  return t*t*t*(t*(t*6.0-15.0)+10.0);
}

// Classic Perlin noise
float cnoise(vec3 P)
{
  vec3 Pi0 = floor(P); // Integer part for indexing
  vec3 Pi1 = Pi0 + vec3(1.0); // Integer part + 1
  Pi0 = mod289(Pi0);
  Pi1 = mod289(Pi1);
  vec3 Pf0 = fract(P); // Fractional part for interpolation
  vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
  vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
  vec4 iy = vec4(Pi0.yy, Pi1.yy);
  vec4 iz0 = Pi0.zzzz;
  vec4 iz1 = Pi1.zzzz;

  vec4 ixy = permute(permute(ix) + iy);
  vec4 ixy0 = permute(ixy + iz0);
  vec4 ixy1 = permute(ixy + iz1);

  vec4 gx0 = ixy0 * (1.0 / 7.0);
  vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
  gx0 = fract(gx0);
  vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
  vec4 sz0 = step(gz0, vec4(0.0));
  gx0 -= sz0 * (step(0.0, gx0) - 0.5);
  gy0 -= sz0 * (step(0.0, gy0) - 0.5);

  vec4 gx1 = ixy1 * (1.0 / 7.0);
  vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
  gx1 = fract(gx1);
  vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
  vec4 sz1 = step(gz1, vec4(0.0));
  gx1 -= sz1 * (step(0.0, gx1) - 0.5);
  gy1 -= sz1 * (step(0.0, gy1) - 0.5);

  vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
  vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
  vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
  vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
  vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
  vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
  vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
  vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);

  vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
  g000 *= norm0.x;
  g010 *= norm0.y;
  g100 *= norm0.z;
  g110 *= norm0.w;
  vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
  g001 *= norm1.x;
  g011 *= norm1.y;
  g101 *= norm1.z;
  g111 *= norm1.w;

  float n000 = dot(g000, Pf0);
  float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
  float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
  float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
  float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
  float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
  float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
  float n111 = dot(g111, Pf1);

  vec3 fade_xyz = fade(Pf0);
  vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
  vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
  float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x); 
  return 2.2 * n_xyz;
}

// Classic Perlin noise, periodic variant
float pnoise(vec3 P, vec3 rep)
{
  vec3 Pi0 = mod(floor(P), rep); // Integer part, modulo period
  vec3 Pi1 = mod(Pi0 + vec3(1.0), rep); // Integer part + 1, mod period
  Pi0 = mod289(Pi0);
  Pi1 = mod289(Pi1);
  vec3 Pf0 = fract(P); // Fractional part for interpolation
  vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
  vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
  vec4 iy = vec4(Pi0.yy, Pi1.yy);
  vec4 iz0 = Pi0.zzzz;
  vec4 iz1 = Pi1.zzzz;

  vec4 ixy = permute(permute(ix) + iy);
  vec4 ixy0 = permute(ixy + iz0);
  vec4 ixy1 = permute(ixy + iz1);

  vec4 gx0 = ixy0 * (1.0 / 7.0);
  vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
  gx0 = fract(gx0);
  vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
  vec4 sz0 = step(gz0, vec4(0.0));
  gx0 -= sz0 * (step(0.0, gx0) - 0.5);
  gy0 -= sz0 * (step(0.0, gy0) - 0.5);

  vec4 gx1 = ixy1 * (1.0 / 7.0);
  vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
  gx1 = fract(gx1);
  vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
  vec4 sz1 = step(gz1, vec4(0.0));
  gx1 -= sz1 * (step(0.0, gx1) - 0.5);
  gy1 -= sz1 * (step(0.0, gy1) - 0.5);

  vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
  vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
  vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
  vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
  vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
  vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
  vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
  vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);

  vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
  g000 *= norm0.x;
  g010 *= norm0.y;
  g100 *= norm0.z;
  g110 *= norm0.w;
  vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
  g001 *= norm1.x;
  g011 *= norm1.y;
  g101 *= norm1.z;
  g111 *= norm1.w;

  float n000 = dot(g000, Pf0);
  float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
  float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
  float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
  float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
  float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
  float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
  float n111 = dot(g111, Pf1);

  vec3 fade_xyz = fade(Pf0);
  vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
  vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
  float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x); 
  return 2.2 * n_xyz;
}

// Include the Ashima code here!
 
varying float noise;
uniform float time;

float turbulence( vec3 p ) {
  float w = 100.0;
  float t = -.5;
  for (float f = 1.0 ; f <= 10.0 ; f++ ){
    float power = pow( 2.0, f );
    t += abs( pnoise( vec3( power * p ), vec3( 10.0, 10.0, 10.0 ) ) / power );
  }
  return t;
}

void main() {

  noise = 10.0 *  -.10 * turbulence( .5 * normal + time );
  float b = 5.0 * pnoise( 0.05 * position + vec3( 2.0 * time ), vec3( 100.0 ) );
  float displacement = - 10. * noise + b;
  
  vec3 newPosition = position + normal * displacement;
  gl_Position = projectionMatrix * modelViewMatrix * vec4( newPosition, 1.0 );

}
</script>

<script type="x-shader/x-vertex" id="perlin_fragmentShader">
varying float noise;
uniform sampler2D tExplosion;

float random( vec3 scale, float seed ){
  return fract( sin( dot( gl_FragCoord.xyz + seed, scale ) ) * 43758.5453 + seed ) ;
}

void main() {

  float r = .01 * random( vec3( 12.9898, 78.233, 151.7182 ), 0.0 );
  vec2 tPos = vec2( 0, 1.0 - 1.3 * noise + r );
  vec4 color = texture2D( tExplosion, tPos );

  gl_FragColor = vec4(color.rgb,0.3 );

}
</script>

	</body>
</html>